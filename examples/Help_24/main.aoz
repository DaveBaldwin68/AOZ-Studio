//
// Help_24.AMOS - Converted to AOZ on the 27/01/2020-16:08:51
//
#manifest:"amiga"
#speed:"safe"

'**********************************
'*    AMOS Professional HELP 24   *               SUBJECTS COVERED
'*                                *
'*     (c) Europress Software     *              Rnd       (line 11)
'*                                *              Randomize (line 25)
'*         Ronnie Simpson         *              Max       (line 41)
'**********************************              Min       (line 52)
'                                                Swap      (line 64)
'                                                Fix       (line 76)
'-------------------------------------------
'Rnd
'-------------------------------------------
'generate a random whole number
'
'eg.        R=Rnd(3)    (R=0,1,2 or 3)
'           A=Rnd(0)    (A=last random number repeated)
'           N=Rnd(5)+1  (this would simulate a dice throw.)
'
'If the number in the brackets is greater than 0 then the random number
'produced will be between 0 and the value given. (inclusive)
'If a value of 0 is placed in the brackets the the last random number
'generated will be repeated. (handy for de-bugging programs)
'
'-------------------------------------------
'Randomize
'-------------------------------------------
'set seed of random number generator
'
'Random numbers are computed internally using a starting point known as the
'seed, this seed is set each time you load AMOS, so the sequence of numbers
'produced would be exactly the same each time you ran a program.
'In some programs this would be unacceptable and the Randomize command
'allows you to set the seed number to any value you wish.
'The best way to produce truly random numbers is to set the seed by the
'internal clock which would be different for each 1/50th of a second that
'the computer had been switched on.
'
'eg.        Randomize Timer
'
'-------------------------------------------
'Max
'-------------------------------------------
'compare two values and return the largest
'
'eg.           M=Max(X,Y)
'              M$=Max(A$,B$)
'              M#=Max(X#,Y#)
'
'The variables or expressions tested must be of the same type or an
'error will be generated.
'-------------------------------------------
'Min
'-------------------------------------------
'compare two values and return the smallest
'
'eg.           M=Min(X,Y)
'              M$=Min(A$,B$)
'              M#=Min(X#,Y#)
'
'As with the Max function the variables or expressions tested must be of
'the same type or an error will be generated.
'
'-------------------------------------------
'Swap
'-------------------------------------------
'swap the contents of two variables
'
'eg.           Swap(X,Y)
'              Swap(A$,B$)
'              Swap(X#,Y#)
'
'The variables or expressions must be of the same type or an error will
'be generated.
'
'-------------------------------------------
'Fix
'-------------------------------------------
'set precision of floating point numbers
'
'Fix changes the way in which floating point numbers will be displayed.
'
'              Fix(x)
'
'There are the four possibilites:-
'
' 1) if x>0 and x<16 then x will denote the number of figures output after
'    the decimal point.
' 2) if x>16 trailing zeros will be removed
' 3) if x=16 the format will be returned to normal
' 4) if x<0 then all floating point numbers will be displayed in exponential
'    format and the absolute value of x will determine the number of figures
'    after the decimal point.
'
'-------------------------------------------
'WORKING EXAMPLE
'-------------------------------------------
Reserve Zone 2
Rem *** tidy up screen and print instructions
'
Screen Open 0,640,200,16,Hires : Flash Off
Palette $0,$F00,$F0,$F,$FF0,$F0F,$FF,$F70,$7F,$70F,$F07,$333,$666,$999,$CCC,$FFF
Curs Off : Cls 0 : Paper 0 : Pen 8
Centre "AMOS Professional RANDOM NUMBERS" : Pen 9
Locate ,2 : Centre "TRY TO PREDICT WHETHER THE NEXT NUMBER WILL BE"
Locate ,3 : Centre "HIGHER OR LOWER THAN THE LAST" : Pen 10
Locate ,5 : Centre "THE NUMBERS RANGE FROM 0-9 INCLUSIVE AND THERE ARE NO DOUBLES" : Pen 1
Locate ,8 : Centre "PRESS RIGHT MOUSE KEY TO QUIT"
Reserve Zone 2
'
'
Randomize Timer
Do
   Pen 4 : Paper 0
   Locate 0,23 : Cline
   For X=2 To 74 Step 8
      Locate X,15 : Print Border$("   ",5)
   Next
   Pen 3 : Paper 6
   Locate 24,20 : Print Border$(Zone$("  HIGHER  ",1),3)
   Locate 44,20 : Print Border$(Zone$("  LOWER  ",2),3)
   X=2 : COUNT=0
   R1=Rnd(9)
   Paper 0
   Repeat
      Inc COUNT
      Pen 7 : Locate 0,12 : Centre "ATTEMPT NUMBER"+Str$(COUNT)
      Repeat
         R2=Rnd(9)
      Until R2<>R1
      Pen 4 : Locate X,15 : Print Border$(Str$(R1)+" ",4)
      Repeat
         M=Mouse Zone
         If Mouse Key=2 Then Edit
      Until Mouse Key=1 and M>0
      Add X,8 : CORRECT=False
      Locate X,15 : Print Border$(Str$(R2)+" ",4)
      If M=1 and Max(R1,R2)=R2
         CORRECT=True : Bell
      End If
      If M=2 and Min(R1,R2)=R2
         CORRECT=True : Bell
      End If
      Swap R1,R2
      Wait 20
   Until COUNT=9 or CORRECT=False
   Locate 0,23 : Pen 2
   If COUNT=9
      Centre "WELL DONE YOU GOT THEM ALL CORRECT!    PRESS ANY MOUSE KEY TO TRY AGAIN"
      For N=20 To 40 : Play 1,N,1 : Next
   Else
      Centre "UNLUCKY THIS TIME!    PRESS ANY MOUSE KEY TO TRY AGAIN"
      For N=20 To 40 : Play 2,41-N,1 : Next
   End If
   Repeat
   Until Mouse Key
Loop
