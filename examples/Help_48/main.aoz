//
// Help_48.AMOS - Converted to AOZ on the 27/01/2020-16:09:01
//
#manifest:"amiga"
#speed:"safe"

'**********************************
'*    AMOS Professional HELP 48   *              SUBJECTS COVERED
'*                                *
'*     (c) Europress Software     *              Rem     (line 10) 
'*                                *              Data    (line 23) 
'*         Ronnie Simpson         *              Read    (line 43) 
'**********************************              Restore (line 58) 
'
'------------------------------------------- 
'Rem or '
'------------------------------------------- 
'make a remark or comment
'
'eg.        Rem ----This line will do nothing
'           '   ----Neither does this one  
'
'The Quote mark version can only be used at the begining of a line while the 
'Rem version can be used at any point. 
'
'eg.        A=2*2 : Rem ----I must not use ' in the middle of a line 
'
'------------------------------------------- 
'Data
'------------------------------------------- 
'list data items 
'
'eg.       Data 1,"TWO",3,"FOUR" 
'
'The data items in the list can be loaded into your variables with the use 
'of the Read instruction.
'The Data statement must be the only instruction on a line (any others will
'be ignored) and each item in the list should be separated by a comma. 
'It is also possible to use expressions in your lists, these will be 
'evaluated at the reading stage before being assigned to your variable.
'
'eg.     Data 2*2,"FOUR",3*3,"SIX" 
'
'Please note that any data statements place inside a procedure will be 
'treated as local to the procedure and will not be accessible from the main
'program.
'
'------------------------------------------- 
'Read
'------------------------------------------- 
'read items from data lists
'  
'eg.       Read A,B$,C$,D,E
'          Print A,B$,C$,D,E 
'          Data 1,"TWO","THREE",4,Rnd(100)+5 
'
'Data items read must be of the same type as required by the variable or 
'an error will be generated. 
'As each item is read the data pointer is automatically moved to the next
'item on the list and an error will be generated if the program runs out 
'of data.
'
'------------------------------------------- 
'Restore 
'------------------------------------------- 
'reset data pointer
'
'Restore will set the data pointer to the given label or line number.
'
'eg.      Restore TEST     (Lable) 
'         Restore 22       (Line number) 
'         Restore A$       (Lable$)
'         Restore B+5      (Expression)
'
'When Restore is used inside a procedure references can only be made to
'items contained inside the current procedure. 
'
'------------------------------------------- 
'WORKING EXAMPLE 
'------------------------------------------- 
Rem *** tidy up the screen 
'
Screen Open 0,320,200,64,Lowres
Flash Off : Curs Off : Cls 0
Palette $0,$100,$200,$300,$400,$500,$600,$700,$800,$900,$A00,$B00,$C00,$D00,$E00,$F00,$0,$10,$20,$30,$40,$50,$60,$70,$80,$90,$A0,$B0,$C0,$D0,$E0,$F0
Gr Writing 0
'
'
Rem *** Start main loop
'
While Mouse Key<2
   X=160 : X1=1
   Y=100 : Y1=1
   Do 
      Rem *** read colour sequence from data 
      Read C
      If C=99
         Rem *** restore to start if out of data
         Restore LABEL_A
         Read C
      End If 
      Inc X1 : Y1=X1/2
      Ink C
      Box X-X1,Y-Y1 To X+X1,Y+Y1
      Exit If X1>190
   Loop 
   Ink 0 : Text 45,190,"HOLD RIGHT MOUSE KEY TO QUIT"
   Rem *** rotate colours 
   Shift Up 2,1,31,1
   For T=1 To 50
      If Mouse Key=2 Then Edit 
      Wait Vbl 
   Next 
   Ink 0
   Rem *** wipe graphics
   While X1>1
      Box X-X1,Y-Y1 To X+X1,Y+Y1
      Dec X1 : Y1=X1/2
      If Mouse Key=2 Then Edit 
   Wend 
   Shift Off 
   Cls 0
   Rem *** call colour change procedure 
   CHANGE
   '
Wend 
'
LABEL_A:
Data 15,14,13,12,11,10,9,8,7,6,5,4,3,2,1,0
Data 32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47
Data 63,62,61,60,59,58,57,56,55,54,53,52,51,50,49,48
Data 16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,99
'
'
Procedure CHANGE
   Restore Rnd(5)+1
   For N=0 To 15
      Read C : Colour N,C
   Next 
   Restore Rnd(5)+1
   For N=16 To 31
      Read C : Colour N,C
   Next 
   '
   Rem *** this data is local to the procedure
   '
   1 Data $0,$1,$2,$3,$4,$5,$6,$7,$8,$9,$A,$B,$C,$D,$E,$F
   2 Data $0,$10,$20,$30,$40,$50,$60,$70,$80,$90,$A0,$B0,$C0,$D0,$E0,$F0
   3 Data $0,$100,$200,$300,$400,$500,$600,$700,$800,$900,$A00,$B00,$C00,$D00,$E00,$F00
   4 Data $0,$11,$22,$33,$44,$55,$66,$77,$88,$99,$AA,$BB,$CC,$DD,$EE,$FF
   5 Data $0,$110,$220,$330,$440,$550,$660,$770,$880,$990,$AA0,$BB0,$CC0,$DD0,$EE0,$FF0
   6 Data $0,$101,$202,$303,$404,$505,$606,$707,$808,$909,$A0A,$B0B,$C0C,$D0D,$E0E,$F0F
End Proc
